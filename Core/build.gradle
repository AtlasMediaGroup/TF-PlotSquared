dependencies {
    testCompile 'junit:junit:4.12'
    compile 'org.yaml:snakeyaml:1.23'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.4'
}

repositories {
    maven { url = "https://hub.spigotmc.org/nexus/content/groups/public/"}
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

processResources {
    from('src/main/resources') {
        include 'plugin.properties'
        expand(
                version: "${project.parent.version}",
                name: project.parent.name,
                commit: "${git.head().abbreviatedId}",
                date: "${git.head().getDate().format("yy.MM.dd")}",
        )
    }
}

jar.archiveName = "Plotsquared-Api-${project.parent.version}.jar"
jar.destinationDir = file '../mvn/com/github/intellectualsites/plotsquared/Plotsquared-Api/' + project.parent.version
task createPom {
    doLast {
        pom {
            project {
                groupId 'com.github.intellectualsites.plotsquared'
                artifactId 'Plotsquared'
                version project.parent.version
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/Plotsquared-Api/${project.parent.version}/Plotsquared-Api-${project.parent.version}.pom")
        pom {
            project {
                groupId 'com.github.intellectualsites.plotsquared'
                artifactId 'Plotsquared'
                version 'latest'
            }
        }.writeTo("../mvn/com/github/intellectualsites/plotsquared/Plotsquared-Api/latest/Plotsquared-Api-latest.pom")
    }
}

task copyFiles {
    doLast {
        copy {
            from "../mvn/com/github/intellectualsites/plotsquared/Plotsquared-Api/${project.parent.version}/"
            into '../mvn/com/github/intellectualsites/plotsquared/Plotsquared-Api/latest/'
            include('*.jar')
            rename("Plotsquared-Api-${project.parent.version}.jar", 'Plotsquared-Api-latest.jar')
        }
    }
}

build.finalizedBy(copyFiles)
copyFiles.dependsOn(createPom)
